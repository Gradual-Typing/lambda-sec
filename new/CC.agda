module CC where

open import Data.Nat
open import Data.Bool renaming (Bool to ğ”¹; _â‰Ÿ_ to _â‰Ÿáµ‡_)
open import Data.List
open import Data.Maybe
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Product using (_Ã—_; âˆƒ; âˆƒ-syntax; Î£; Î£-syntax) renaming (_,_ to âŸ¨_,_âŸ©)
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl)
open import Function using (case_of_)

open import Types
open import TypeBasedCast
open import CCSyntax Cast_â‡’_ public
open import CCTyping Cast_â‡’_ public
open import Utils


data Value : Term â†’ Set where
  V-addr : âˆ€ {a â„“} â†’ Value (addr a of â„“)
  V-Æ› : âˆ€ {gc A N â„“} â†’ Value (Æ›[ gc ] A Ë™ N of â„“)
  V-const : âˆ€ {Î¹} {k : rep Î¹} {â„“} â†’ Value ($ k of â„“)
  V-cast : âˆ€ {A B V} {c : Cast A â‡’ B}
    â†’ Value V â†’ Inert c â†’ Value (V âŸ¨ c âŸ©)

data Err : Term â†’ Set where
  E-error : âˆ€ {e : Error} â†’ Err (error e)

-- The labels on a constant and its type are related by subtyping.
const-label-â‰¼ : âˆ€ {Î“ Î£ gc Î¹} {k : rep Î¹} {â„“ g}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ $ k of â„“ â¦‚ ` Î¹ of g
  â†’ âˆƒ[ â„“â€² ] (g â‰¡ l â„“â€²) Ã— (â„“ â‰¼ â„“â€²)
const-label-â‰¼ {â„“ = â„“} âŠ¢const = âŸ¨ â„“ , refl , â‰¼-refl âŸ©
const-label-â‰¼ (âŠ¢sub âŠ¢M (<:-ty â„“â€²<:g <:-Î¹)) =
  case âŸ¨ const-label-â‰¼ âŠ¢M , â„“â€²<:g âŸ© of Î» where
    âŸ¨ âŸ¨ â„“â€² , refl , â„“â‰¼â„“â€² âŸ© , <:-l â„“â€²â‰¼â„“â€³ âŸ© â†’
      âŸ¨ _ , refl , â‰¼-trans â„“â‰¼â„“â€² â„“â€²â‰¼â„“â€³ âŸ©
const-label-â‰¼ (âŠ¢sub-pc âŠ¢M gc<:gcâ€²) = const-label-â‰¼ âŠ¢M

-- The type on a cast and its type are related by subtyping.
cast-<: : âˆ€ {Î“ Î£ gc A B Bâ€² M} {c : Cast A â‡’ B}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ M âŸ¨ c âŸ© â¦‚ Bâ€²
  â†’ B <: Bâ€²
cast-<: (âŠ¢cast âŠ¢Mc) = <:-refl
cast-<: (âŠ¢sub âŠ¢Mc Bâ€³<:Bâ€²) = let B<:Bâ€³ = cast-<: âŠ¢Mc in <:-trans B<:Bâ€³ Bâ€³<:Bâ€²
cast-<: (âŠ¢sub-pc âŠ¢Mc gc<:gcâ€³) = cast-<: âŠ¢Mc

data Fun : Term â†’ HeapContext â†’ Type â†’ Set where
  Fun-Æ› : âˆ€ {Î£ gc pc A Aâ€² B Bâ€² g N â„“}
    â†’ (Aâ€² âˆ· []) Í¾ Î£ Í¾ l pc âŠ¢ N â¦‚ Bâ€²
    â†’ [ l pc ] Aâ€² â‡’ Bâ€² of l â„“ <: [ gc ] A â‡’ B of g
      ----------------------------------------------------- Lambda
    â†’ Fun (Æ›[ pc ] Aâ€² Ë™ N of â„“) Î£ ([ gc ] A â‡’ B of g)

  Fun-proxy : âˆ€ {Î£ gc gcâ‚ gcâ‚‚ A Aâ‚ Aâ‚‚ B Bâ‚ Bâ‚‚ g gâ‚ gâ‚‚ V}
                {c : Cast ([ gcâ‚ ] Aâ‚ â‡’ Bâ‚ of gâ‚) â‡’ ([ gcâ‚‚ ] Aâ‚‚ â‡’ Bâ‚‚ of gâ‚‚)}
    â†’ Fun V Î£ ([ gcâ‚ ] Aâ‚ â‡’ Bâ‚ of gâ‚)
    â†’ Inert c
    â†’ [ gcâ‚‚ ] Aâ‚‚ â‡’ Bâ‚‚ of gâ‚‚ <: [ gc ] A â‡’ B of g
      ----------------------------------------------------- Function Proxy
    â†’ Fun (V âŸ¨ c âŸ©) Î£ ([ gc ] A â‡’ B of g)

-- Sanity checks
fun-is-value : âˆ€ {Î£ V gc A B g}
  â†’ Fun V Î£ ([ gc ] A â‡’ B of g)
  â†’ Value V
fun-is-value (Fun-Æ› _ sub) = V-Æ›
fun-is-value (Fun-proxy fun i _) = V-cast (fun-is-value fun) i

fun-wt : âˆ€ {Î£ V gc gcâ€² A B g}
  â†’ Fun V Î£ ([ gcâ€² ] A â‡’ B of g)
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ [ gcâ€² ] A â‡’ B of g
fun-wt (Fun-Æ› {Î£} âŠ¢N sub) = âŠ¢sub (âŠ¢lam âŠ¢N) sub
fun-wt (Fun-proxy fun i sub) = âŠ¢sub (âŠ¢cast (fun-wt fun)) sub

-- Canonical form of value of function type
canonical-fun : âˆ€ {Î£ gc gcâ€² A B g V}
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ [ gcâ€² ] A â‡’ B of g
  â†’ Value V
  â†’ Fun V Î£ ([ gcâ€² ] A â‡’ B of g)
canonical-fun (âŠ¢lam âŠ¢N) V-Æ› = Fun-Æ› âŠ¢N <:-refl
canonical-fun (âŠ¢cast âŠ¢V) (V-cast v (I-fun c iâ‚ iâ‚‚)) =
  Fun-proxy (canonical-fun âŠ¢V v) (I-fun c iâ‚ iâ‚‚) <:-refl
canonical-fun (âŠ¢sub âŠ¢V sub) v =
  case sub of Î» where
    (<:-ty _ (<:-fun _ _ _)) â†’
      case canonical-fun âŠ¢V v of Î» where
        (Fun-Æ› âŠ¢N subâ‚)        â†’ Fun-Æ› âŠ¢N (<:-trans subâ‚ sub)
        (Fun-proxy fun i subâ‚) â†’ Fun-proxy fun i (<:-trans subâ‚ sub)
canonical-fun (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = canonical-fun âŠ¢V v

data Reference : Term â†’ HeapContext â†’ Type â†’ Set where
  Ref-addr : âˆ€ {Î£ A Aâ€² a g â„“}
    â†’ key _â‰Ÿ_ Î£ a â‰¡ just Aâ€²
    â†’ Ref Aâ€² of l â„“ <: Ref A of g
      ---------------------------------------- Reference
    â†’ Reference (addr a of â„“) Î£ (Ref A of g)

  Ref-proxy : âˆ€ {Î£ A Aâ‚ Aâ‚‚ g gâ‚ gâ‚‚ V} {c : Cast (Ref Aâ‚ of gâ‚) â‡’ (Ref Aâ‚‚ of gâ‚‚)}
    â†’ Reference V Î£ (Ref Aâ‚ of gâ‚)
    â†’ Inert c
    â†’ Ref Aâ‚‚ of gâ‚‚ <: Ref A of g
      ---------------------------------------- Reference proxy
    â†’ Reference (V âŸ¨ c âŸ©) Î£ (Ref A of g)

ref-is-value : âˆ€ {Î£ V A g}
  â†’ Reference V Î£ (Ref A of g)
  â†’ Value V
ref-is-value (Ref-addr _ _) = V-addr
ref-is-value (Ref-proxy ref i _) = V-cast (ref-is-value ref) i

canonical-ref : âˆ€ {Î£ gc A g V}
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ Ref A of g
  â†’ Value V
  â†’ Reference V Î£ (Ref A of g)
canonical-ref (âŠ¢addr eq) V-addr = Ref-addr eq <:-refl
canonical-ref (âŠ¢cast âŠ¢V) (V-cast v (I-ref c i)) =
  Ref-proxy (canonical-ref âŠ¢V v) (I-ref c i) <:-refl
canonical-ref (âŠ¢sub âŠ¢V sub) v =
  case sub of Î» where
    (<:-ty _ (<:-ref _ _)) â†’
      case canonical-ref âŠ¢V v of Î» where
        (Ref-addr eq subâ‚) â†’ Ref-addr eq (<:-trans subâ‚ sub)
        (Ref-proxy ref i subâ‚) â†’ Ref-proxy ref i (<:-trans subâ‚ sub)
canonical-ref (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = canonical-ref âŠ¢V v

data Constant : Term â†’ Base â†’ Set where
  Const-base : âˆ€ {Î¹} {k : rep Î¹} {â„“}
      ------------------------------- Constant
    â†’ Constant ($ k of â„“) Î¹

  Const-inj : âˆ€ {Î¹} {k : rep Î¹} {â„“ â„“â€²} {c : Cast (` Î¹ of l â„“) â‡’ (` Î¹ of â‹†)}
    â†’ â„“â€² â‰¼ â„“
      ------------------------------- Injected constant
    â†’ Constant ($ k of â„“â€² âŸ¨ c âŸ©) Î¹

canonical-const : âˆ€ {Î£ gc Î¹ g V}
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ ` Î¹ of g
  â†’ Value V
  â†’ Constant V Î¹
canonical-const âŠ¢const V-const = Const-base
canonical-const (âŠ¢cast âŠ¢V) (V-cast v (I-base-inj c)) =
  case canonical-const âŠ¢V v of Î» where
    Const-base â†’
      case const-label-â‰¼ âŠ¢V of Î» where
        âŸ¨ â„“â€² , refl , â„“â‰¼â„“â€² âŸ© â†’ Const-inj â„“â‰¼â„“â€²
    (Const-inj _) â†’ case cast-<: âŠ¢V of Î» where (<:-ty () <:-Î¹)
canonical-const (âŠ¢sub âŠ¢V (<:-ty _ <:-Î¹)) v = canonical-const âŠ¢V v
canonical-const (âŠ¢sub-pc âŠ¢V _) v = canonical-const âŠ¢V v

canonicalâ‹† : âˆ€ {Î“ Î£ gc V T}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ T of â‹†
  â†’ Value V
  â†’ âˆƒ[ A ] âˆƒ[ B ] Î£[ c âˆˆ Cast A â‡’ B ] âˆƒ[ W ]
       (V â‰¡ W âŸ¨ c âŸ©) Ã— (Inert c) Ã— (B <: T of â‹†)
canonicalâ‹† (âŠ¢cast âŠ¢W) (V-cast {V = W} {c} w i) =
  âŸ¨ _ , _ , c , W , refl , i , <:-ty <:-â‹† <:áµ£-refl âŸ©
canonicalâ‹† (âŠ¢sub âŠ¢V (<:-ty {S = Tâ€²} <:-â‹† Tâ€²<:T)) v =
  case canonicalâ‹† âŠ¢V v of Î» where
    âŸ¨ A , B , c , W , refl , i , B<:Tâ€²â‹† âŸ© â†’
      âŸ¨ A , B , c , W , refl , i , <:-trans B<:Tâ€²â‹† (<:-ty <:-â‹† Tâ€²<:T) âŸ©
canonicalâ‹† (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = canonicalâ‹† âŠ¢V v

canonical-pcâ‹† : âˆ€ {Î“ Î£ gc V A B g}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ [ â‹† ] A â‡’ B of g
  â†’ Value V
  â†’ âˆƒ[ C ] âˆƒ[ D ] Î£[ c âˆˆ Cast C â‡’ D ] âˆƒ[ W ]
       (V â‰¡ W âŸ¨ c âŸ©) Ã— (Inert c) Ã— (D <: [ â‹† ] A â‡’ B of g)
canonical-pcâ‹† (âŠ¢cast âŠ¢W) (V-cast {V = W} {c} w i) =
  âŸ¨ _ , _ , c , W , refl , i , <:-refl âŸ©
canonical-pcâ‹† (âŠ¢sub âŠ¢V (<:-ty gâ€²<:g (<:-fun <:-â‹† A<:Aâ€² Bâ€²<:B))) v =
  case canonical-pcâ‹† âŠ¢V v of Î» where
    âŸ¨ C , D , c , W , refl , i , D<:Aâ€²â†’Bâ€² âŸ© â†’
      let D<:Aâ†’B = <:-trans D<:Aâ€²â†’Bâ€² (<:-ty gâ€²<:g (<:-fun <:-â‹† A<:Aâ€² Bâ€²<:B)) in
        âŸ¨ C , D , c , W , refl , i , D<:Aâ†’B âŸ©
canonical-pcâ‹† (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = canonical-pcâ‹† âŠ¢V v

apply-cast : âˆ€ {Î“ Î£ gc A B} â†’ (V : Term) â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ A â†’ Value V â†’ (c : Cast A â‡’ B) â†’ Active c â†’ Term
-- V âŸ¨ ` Î¹ of g â‡’ ` Î¹ of g âŸ© â€”â†’ V
apply-cast V âŠ¢V v c (A-base-id .c) = V
apply-cast V âŠ¢V v c (A-base-proj (cast (` Î¹ of â‹†) (` Î¹ of l â„“) p (~-ty â‹†~ ~-Î¹))) =
  case canonicalâ‹† âŠ¢V v of Î» where
    âŸ¨ _ , _ , cast (` Î¹ of l â„“â€²) (` Î¹ of â‹†) q (~-ty ~â‹† ~-Î¹) ,
      W , refl , I-base-inj _ , <:-ty <:-â‹† <:-Î¹ âŸ© â†’
      case â„“â€² â‰¼? â„“ of Î» where
        (yes _) â†’ V
        (no _) â†’ error (blame p)
{- Order of reduction: propagate label first and then the security effect
        V âŸ¨ [ pcâ€² ] Aâ‚ â†’ Aâ‚‚ of â„“â€² â‡’ [ â‹† ] Bâ‚ â†’ Bâ‚‚ of â‹† âŸ© âŸ¨ [ â‹† ] Câ‚ â†’ Câ‚‚ of â‹† â‡’ [ pc ] Dâ‚ â†’ Dâ‚‚ of â„“ âŸ©
   â€”â†’ V âŸ¨ [ pcâ€² ] Aâ‚ â†’ Aâ‚‚ of â„“  â‡’ [ â‹† ] Bâ‚ â†’ Bâ‚‚ of â„“ âŸ© âŸ¨ [ â‹† ] Câ‚ â†’ Câ‚‚ of â„“ â‡’ [ pc ] Dâ‚ â†’ Dâ‚‚ of â„“ âŸ©   , if â„“â€² â‰¼ â„“
   â€”â†’ V âŸ¨ [ pc  ] Aâ‚ â†’ Aâ‚‚ of â„“  â‡’ [ pc ] Bâ‚ â†’ Bâ‚‚ of â„“ âŸ© âŸ¨ [ pc ] Câ‚ â†’ Câ‚‚ of â„“ â‡’ [ pc ] Dâ‚ â†’ Dâ‚‚ of â„“ âŸ© , if pc â‰¼ pcâ€²
   -}
apply-cast V âŠ¢V v c (A-fun (cast ([ gcâ‚ ] Câ‚ â‡’ Câ‚‚ of â‹†) ([ gcâ‚‚ ] Dâ‚ â‡’ Dâ‚‚ of g) p (~-ty _ C~D)) a) =
  case canonicalâ‹† âŠ¢V v of Î» where
    âŸ¨ _ , _ , cast ([ gcâ‚â€² ] Aâ‚ â‡’ Aâ‚‚ of l â„“â€²) ([ gcâ‚‚â€² ] Bâ‚ â‡’ Bâ‚‚ of â‹†) q (~-ty ~â‹† A~B) ,
      W , refl , I-fun _ I-label I-label , <:-ty <:-â‹† (<:-fun gcâ‚<:gcâ‚‚â€² Câ‚<:Bâ‚ Bâ‚‚<:Câ‚‚) âŸ© â†’
      case a of Î» where
        --      W âŸ¨ [ gcâ‚â€² ] Aâ‚ â†’ Aâ‚‚ of â„“â€² â‡’ [ gcâ‚‚â€² ] Bâ‚ â†’ Bâ‚‚ of â‹†  âŸ© âŸ¨ [ gcâ‚ ] Câ‚ â†’ Câ‚‚ of â‹†  â‡’ [ gcâ‚‚ ] Dâ‚ â†’ Dâ‚‚ of â‹† âŸ©
        -- â€”â†’ W âŸ¨ [ gcâ‚â€² ] Aâ‚ â†’ Aâ‚‚ of â„“â€² â‡’ [ gcâ‚‚â€² ] Bâ‚ â†’ Bâ‚‚ of â„“â€² âŸ© âŸ¨ [ gcâ‚ ] Câ‚ â†’ Câ‚‚ of â„“â€² â‡’ [ gcâ‚‚ ] Dâ‚ â†’ Dâ‚‚ of â‹† âŸ©
        A-idâ‹† â†’
          let c~â‚ = ~-ty ~â‚—-refl A~B
              c~â‚‚ = ~-ty ~â‹†      C~D in
            W âŸ¨ cast ([ gcâ‚â€² ] Aâ‚ â‡’ Aâ‚‚ of l â„“â€²) ([ gcâ‚‚â€² ] Bâ‚ â‡’ Bâ‚‚ of l â„“â€²) q c~â‚ âŸ©
              âŸ¨ cast ([ gcâ‚  ] Câ‚ â‡’ Câ‚‚ of l â„“â€²) ([ gcâ‚‚  ] Dâ‚ â‡’ Dâ‚‚ of â‹†   ) p c~â‚‚ âŸ©
        --      W âŸ¨ [ gcâ‚â€² ] Aâ‚ â†’ Aâ‚‚ of â„“â€² â‡’ [ gcâ‚‚â€² ] Bâ‚ â†’ Bâ‚‚ of â‹† âŸ© âŸ¨ [ gcâ‚ ] Câ‚ â†’ Câ‚‚ of â‹† â‡’ [ gcâ‚‚ ] Dâ‚ â†’ Dâ‚‚ of â„“ âŸ©
        -- â€”â†’ W âŸ¨ [ gcâ‚â€² ] Aâ‚ â†’ Aâ‚‚ of â„“  â‡’ [ gcâ‚‚â€² ] Bâ‚ â†’ Bâ‚‚ of â„“ âŸ© âŸ¨ [ gcâ‚ ] Câ‚ â†’ Câ‚‚ of â„“ â‡’ [ gcâ‚‚ ] Dâ‚ â†’ Dâ‚‚ of â„“ âŸ©  , if â„“â€² â‰¼ â„“
        --      blame p  , otherwise
        (A-proj {â„“}) â†’
          case â„“â€² â‰¼? â„“ of Î» where
            (yes _) â†’
              let c~â‚ = ~-ty ~â‚—-refl A~B
                  c~â‚‚ = ~-ty ~â‚—-refl C~D in
                W âŸ¨ cast ([ gcâ‚â€² ] Aâ‚ â‡’ Aâ‚‚ of l â„“) ([ gcâ‚‚â€² ] Bâ‚ â‡’ Bâ‚‚ of l â„“) q c~â‚ âŸ©
                  âŸ¨ cast ([ gcâ‚  ] Câ‚ â‡’ Câ‚‚ of l â„“) ([ gcâ‚‚  ] Dâ‚ â‡’ Dâ‚‚ of l â„“) p c~â‚‚ âŸ©
            (no _) â†’ error (blame p)
apply-cast V âŠ¢V v c (A-fun-pc (cast ([ â‹† ] Câ‚ â‡’ Câ‚‚ of gâ‚) ([ gc ] Dâ‚ â‡’ Dâ‚‚ of gâ‚‚) p (~-ty gâ‚~gâ‚‚ (~-fun _ Câ‚~Dâ‚ Câ‚‚~Dâ‚‚))) a I-label) =
  case canonical-pcâ‹† âŠ¢V v of Î» where
    âŸ¨ _ , _ , cast ([ l pcâ€² ] Aâ‚ â‡’ Aâ‚‚ of gâ‚â€²) ([ â‹† ] Bâ‚ â‡’ Bâ‚‚ of gâ‚‚â€²) q (~-ty gâ‚â€²~gâ‚‚â€² (~-fun ~â‹† Aâ‚~Bâ‚ Aâ‚‚~Bâ‚‚)) ,
      W , refl , I-fun _ I-label I-label , <:-ty _ (<:-fun <:-â‹† _ _) âŸ© â†’
      case a of Î» where
        --      W âŸ¨ [ pcâ€² ] Aâ‚ â†’ Aâ‚‚ of gâ‚â€² â‡’ [ â‹†   ] Bâ‚ â†’ Bâ‚‚ of gâ‚‚â€² âŸ© âŸ¨ [ â‹†   ] Câ‚ â†’ Câ‚‚ of gâ‚ â‡’ [ â‹† ] Dâ‚ â†’ Dâ‚‚ of gâ‚‚ âŸ©
        -- â€”â†’ W âŸ¨ [ pcâ€² ] Aâ‚ â†’ Aâ‚‚ of gâ‚â€² â‡’ [ pcâ€² ] Bâ‚ â†’ Bâ‚‚ of gâ‚‚â€² âŸ© âŸ¨ [ pcâ€² ] Câ‚ â†’ Câ‚‚ of gâ‚ â‡’ [ â‹† ] Dâ‚ â†’ Dâ‚‚ of gâ‚‚ âŸ©
        A-idâ‹† â†’
          let c~â‚ = ~-ty gâ‚â€²~gâ‚‚â€² (~-fun ~â‚—-refl Aâ‚~Bâ‚ Aâ‚‚~Bâ‚‚)
              c~â‚‚ = ~-ty gâ‚~gâ‚‚ (~-fun ~â‹† Câ‚~Dâ‚ Câ‚‚~Dâ‚‚) in
            W âŸ¨ cast ([ l pcâ€² ] Aâ‚ â‡’ Aâ‚‚ of gâ‚â€²) ([ l pcâ€² ] Bâ‚ â‡’ Bâ‚‚ of gâ‚‚â€²) q c~â‚ âŸ©
              âŸ¨ cast ([ l pcâ€² ] Câ‚ â‡’ Câ‚‚ of gâ‚) ([ â‹† ] Dâ‚ â‡’ Dâ‚‚ of gâ‚‚) p c~â‚‚ âŸ©
        --      W âŸ¨ [ pcâ€² ] Aâ‚ â†’ Aâ‚‚ of gâ‚â€² â‡’ [ â‹†  ] Bâ‚ â†’ Bâ‚‚ of gâ‚‚â€² âŸ© âŸ¨ [ â‹†  ] Câ‚ â†’ Câ‚‚ of gâ‚ â‡’ [ pc ] Dâ‚ â†’ Dâ‚‚ of gâ‚‚ âŸ©
        -- â€”â†’ W âŸ¨ [ pc  ] Aâ‚ â†’ Aâ‚‚ of gâ‚â€² â‡’ [ pc ] Bâ‚ â†’ Bâ‚‚ of gâ‚‚â€² âŸ© âŸ¨ [ pc ] Câ‚ â†’ Câ‚‚ of gâ‚ â‡’ [ pc ] Dâ‚ â†’ Dâ‚‚ of gâ‚‚ âŸ©  , if pc â‰¼ pcâ€²
        --      blame p  , otherwise
        (A-proj {pc}) â†’
          case pc â‰¼? pcâ€² of Î» where
            (yes _) â†’
              let c~â‚ = ~-ty gâ‚â€²~gâ‚‚â€² (~-fun ~â‚—-refl Aâ‚~Bâ‚ Aâ‚‚~Bâ‚‚)
                  c~â‚‚ = ~-ty gâ‚~gâ‚‚ (~-fun ~â‚—-refl Câ‚~Dâ‚ Câ‚‚~Dâ‚‚) in
              W âŸ¨ cast ([ l pc ] Aâ‚ â‡’ Aâ‚‚ of gâ‚â€²) ([ l pc ] Bâ‚ â‡’ Bâ‚‚ of gâ‚‚â€²) q c~â‚ âŸ©
                âŸ¨ cast ([ l pc ] Câ‚ â‡’ Câ‚‚ of gâ‚ ) ([ l pc ] Dâ‚ â‡’ Dâ‚‚ of gâ‚‚ ) p c~â‚‚ âŸ©
            (no _) â†’ error (blame p)
apply-cast V âŠ¢V v c (A-ref (cast (Ref C of â‹†) (Ref D of g) p (~-ty _ RefC~RefD)) a) =
  case canonicalâ‹† âŠ¢V v of Î» where
    âŸ¨ _ , _ , cast (Ref A of l â„“â€²) (Ref B of â‹†) q (~-ty ~â‹† RefA~RefB) ,
      W , refl , I-ref _ I-label , <:-ty <:-â‹† (<:-ref B<:C C<:B) âŸ© â†’
      case a of Î» where
        --      W âŸ¨ Ref A of â„“â€² â‡’ Ref B of â‹†  âŸ© âŸ¨ Ref C of â‹†  â‡’ Ref D of â‹† âŸ©
        -- â€”â†’ W âŸ¨ Ref A of â„“â€² â‡’ Ref B of â„“â€² âŸ© âŸ¨ Ref C of â„“â€² â‡’ Ref D of â‹† âŸ©
        A-idâ‹† â†’
          let c~â‚ = ~-ty ~â‚—-refl RefA~RefB
              c~â‚‚ = ~-ty ~â‹†      RefC~RefD in
            W âŸ¨ cast (Ref A of l â„“â€²) (Ref B of l â„“â€²) q c~â‚ âŸ© âŸ¨ cast (Ref C of l â„“â€²) (Ref D of â‹†) p c~â‚‚ âŸ©
        --      W âŸ¨ Ref A of â„“â€² â‡’ Ref B of â‹† âŸ© âŸ¨ Ref C of â‹† â‡’ Ref D of â„“ âŸ©
        -- â€”â†’ W âŸ¨ Ref A of â„“  â‡’ Ref B of â„“ âŸ© âŸ¨ Ref C of â„“ â‡’ Ref D of â„“ âŸ©  , if â„“â€² â‰¼ â„“
        --      blame p                                                       , otherwise
        (A-proj {â„“}) â†’
          case â„“â€² â‰¼? â„“ of Î» where
            (yes _) â†’
              let c~â‚ = ~-ty ~â‚—-refl RefA~RefB
                  c~â‚‚ = ~-ty ~â‚—-refl RefC~RefD in
                W âŸ¨ cast (Ref A of l â„“) (Ref B of l â„“) q c~â‚ âŸ© âŸ¨ cast (Ref C of l â„“) (Ref D of l â„“) p c~â‚‚ âŸ©
            (no _) â†’ error (blame p)


{- NOTE:
   Categorizing by PC, there are two types of _inert_ function casts:
     1) [ pc ] A â†’ B of â„“â‚ â‡’ [ pc ] C â†’ D of gâ‚‚
     2) [ pc ] A â†’ B of â„“â‚ â‡’ [ â‹†  ] C â†’ D of gâ‚‚
   -}
elim-fun-cast : âˆ€ {A B C D gcâ‚ gcâ‚‚ gâ‚ gâ‚‚} {c : Cast ([ gcâ‚ ] A â‡’ B of gâ‚) â‡’ ([ gcâ‚‚ ] C â‡’ D of gâ‚‚)}
  â†’ (V W : Term) â†’ (pc : StaticLabel) â†’ Inert c â†’ Term
elim-fun-cast {c = c} V W pc (I-fun (cast ([ l pcâ‚ ] A â‡’ B of l â„“â‚) ([ l pcâ‚‚ ] C â‡’ D of gâ‚‚) p _) I-label I-label) =
  (V Â· (W âŸ¨ dom c âŸ©)) âŸ¨ cod c âŸ©
elim-fun-cast {c = c} V W pc (I-fun (cast ([ l pcâ‚ ] A â‡’ B of l â„“â‚) ([ â‹† ] C â‡’ D of gâ‚‚) p _) I-label I-label) =
  case (pc â‹ â„“â‚) â‰¼? pcâ‚ of Î» where
    (yes _) â†’ cast-pc pc (V Â· (W âŸ¨ dom c âŸ©)) âŸ¨ cod c âŸ©
    (no _)  â†’ error (blame p)


-- A helper function to unwrap (inert) casts around a value
unwrap : âˆ€ V â†’ Value V â†’ Term
unwrap (V âŸ¨ c âŸ©) (V-cast v i) = unwrap V v
unwrap V _ = V

unwrap-ref : âˆ€ {Î“ Î£ gc V A g}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ Ref A of g
  â†’ (v : Value V)
  â†’ âˆƒ[ a ] âˆƒ[ â„“ ] (unwrap V v â‰¡ addr a of â„“) Ã—
                   (âˆƒ[ Aâ€² ] Î“ Í¾ Î£ Í¾ gc âŠ¢ addr a of â„“ â¦‚ Ref Aâ€² of l â„“)
unwrap-ref (âŠ¢addr eq) V-addr = âŸ¨ _ , _ , refl , _ , âŠ¢addr eq âŸ©
unwrap-ref (âŠ¢cast âŠ¢V) (V-cast {c = cast A B _ (~-ty _ (~-ref _))} v i) =
  unwrap-ref âŠ¢V v
unwrap-ref (âŠ¢sub âŠ¢V (<:-ty _ (<:-ref A<:B B<:A))) v
  rewrite <:-antisym A<:B B<:A = unwrap-ref âŠ¢V v
unwrap-ref (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v =
  let âŸ¨ a , â„“ , eq , Aâ€² , âŠ¢a âŸ© = unwrap-ref âŠ¢V v in
    âŸ¨ a , â„“ , eq , Aâ€² , âŠ¢sub-pc âŠ¢a gc<:gcâ€² âŸ©

stamp-inert : âˆ€ {A B} â†’ (c : Cast A â‡’ B) â†’ Inert c â†’ âˆ€ â„“
                      â†’ (Cast (stamp A (l â„“)) â‡’ (stamp B (l â„“)))
stamp-inert (cast (` Î¹ of l â„“â‚) (` Î¹ of â‹†) p (~-ty ~â‹† ~-Î¹))
            (I-base-inj _) â„“ =
  cast (` Î¹ of l (â„“â‚ â‹ â„“)) (` Î¹ of â‹†) p (~-ty ~â‹† ~-Î¹)
stamp-inert (cast ([ gcâ‚ ] A â‡’ B of gâ‚) ([ gcâ‚‚ ] C â‡’ D of gâ‚‚) p (~-ty gâ‚~gâ‚‚ Aâ†’B~Câ†’D))
            (I-fun _ I-label I-label) â„“ =
  let c~ = ~-ty (consis-join-~â‚— gâ‚~gâ‚‚ ~â‚—-refl) Aâ†’B~Câ†’D in
    cast ([ gcâ‚ ] A â‡’ B of (gâ‚ â‹Ìƒ l â„“)) ([ gcâ‚‚ ] C â‡’ D of (gâ‚‚ â‹Ìƒ l â„“)) p c~
stamp-inert (cast (Ref A of gâ‚) (Ref B of gâ‚‚) p (~-ty gâ‚~gâ‚‚ RefA~RefB))
            (I-ref _ I-label) â„“ =
  let c~ = ~-ty (consis-join-~â‚— gâ‚~gâ‚‚ ~â‚—-refl) RefA~RefB in
    cast (Ref A of (gâ‚ â‹Ìƒ l â„“)) (Ref B of (gâ‚‚ â‹Ìƒ l â„“)) p c~

stamp-val : âˆ€ V â†’ Value V â†’ StaticLabel â†’ Term
stamp-val (addr a of â„“â‚) V-addr â„“ = addr a of (â„“â‚ â‹ â„“)
stamp-val (Æ›[ gc ] A Ë™ N of â„“â‚) V-Æ› â„“ = Æ›[ gc ] A Ë™ N of (â„“â‚ â‹ â„“)
stamp-val ($ k of â„“â‚) V-const â„“ = $ k of (â„“â‚ â‹ â„“)
stamp-val (V âŸ¨ c âŸ©) (V-cast v i) â„“ = stamp-val V v â„“ âŸ¨ stamp-inert c i â„“ âŸ©

-- Value stamping is well-typed
stamp-val-wt : âˆ€ {Î“ Î£ gc V A â„“}
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ A
  â†’ (v : Value V)
  â†’ Î“ Í¾ Î£ Í¾ gc âŠ¢ stamp-val V v â„“ â¦‚ stamp A (l â„“)
stamp-val-wt (âŠ¢addr eq) V-addr = âŠ¢addr eq
stamp-val-wt (âŠ¢lam âŠ¢N) V-Æ› = âŠ¢lam âŠ¢N
stamp-val-wt âŠ¢const V-const = âŠ¢const
stamp-val-wt (âŠ¢cast âŠ¢V) (V-cast v i) = âŠ¢cast (stamp-val-wt âŠ¢V v)
stamp-val-wt (âŠ¢sub âŠ¢V A<:B) v = âŠ¢sub (stamp-val-wt âŠ¢V v) (stamp-<: A<:B <:â‚—-refl)
stamp-val-wt (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = âŠ¢sub-pc (stamp-val-wt âŠ¢V v) gc<:gcâ€²

-- A stamped value is value
stamp-inert-inert : âˆ€ {A B} {c : Cast A â‡’ B} {â„“}
  â†’ (i : Inert c)
  â†’ Inert (stamp-inert c i â„“)
stamp-inert-inert (I-base-inj c) = I-base-inj _
stamp-inert-inert (I-fun c I-label I-label) =
  I-fun (stamp-inert c _ _) I-label I-label
stamp-inert-inert (I-ref c I-label) =
  I-ref (stamp-inert c _ _) I-label

stamp-val-value : âˆ€ {V â„“}
  â†’ (v : Value V)
  â†’ Value (stamp-val V v â„“)
stamp-val-value V-addr = V-addr
stamp-val-value V-Æ› = V-Æ›
stamp-val-value V-const = V-const
stamp-val-value (V-cast v i) =
  V-cast (stamp-val-value v) (stamp-inert-inert i)

âŠ¢value-gc : âˆ€ {Î£ gc gcâ€² V A}
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ V â¦‚ A
  â†’ Value V
  â†’ [] Í¾ Î£ Í¾ gcâ€² âŠ¢ V â¦‚ A
âŠ¢value-gc (âŠ¢addr eq) V-addr = âŠ¢addr eq
âŠ¢value-gc (âŠ¢lam âŠ¢N) V-Æ› = âŠ¢lam âŠ¢N
âŠ¢value-gc âŠ¢const V-const = âŠ¢const
âŠ¢value-gc (âŠ¢cast âŠ¢V) (V-cast v i) = âŠ¢cast (âŠ¢value-gc âŠ¢V v)
âŠ¢value-gc (âŠ¢sub âŠ¢V A<:B) v = âŠ¢sub (âŠ¢value-gc âŠ¢V v) A<:B
âŠ¢value-gc (âŠ¢sub-pc âŠ¢V gc<:gcâ€²) v = âŠ¢value-gc âŠ¢V v

-- If an address is well-typed, the heap context lookup is successful.
-- (inversion on the typing derivation of an address)
âŠ¢addr-lookup : âˆ€ {Î£ gc a â„“ A g}
  â†’ [] Í¾ Î£ Í¾ gc âŠ¢ addr a of â„“ â¦‚ Ref A of g
  â†’ key _â‰Ÿ_ Î£ a â‰¡ just A
âŠ¢addr-lookup âŠ¢a =
 case canonical-ref âŠ¢a V-addr of Î» where
    (Ref-addr eq (<:-ty _ (<:-ref Aâ€²<:A A<:Aâ€²))) â†’
      case <:-antisym Aâ€²<:A A<:Aâ€² of Î» where refl â†’ eq
